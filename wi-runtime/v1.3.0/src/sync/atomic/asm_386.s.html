<!DOCTYPE html>
<html>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<meta name="viewport" content="width=device-width, initial-scale=1">
<meta name="theme-color" content="#375EAB">

  <title>src/sync/atomic/asm_386.s - The Go Programming Language</title>

<link type="text/css" rel="stylesheet" href="../../../lib/godoc/style.css">

<link rel="stylesheet" href="../../../lib/godoc/jquery.treeview.css">
<script type="text/javascript">window.initFuncs = [];</script>
</head>
<body>

<div id='lowframe' style="position: fixed; bottom: 0; left: 0; height: 0; width: 100%; border-top: thin solid grey; background-color: white; overflow: auto;">
...
</div><!-- #lowframe -->

<div id="topbar" class="wide"><div class="container">
<div class="top-heading" id="heading-wide"><a href="http://localhost:6060/">The Go Programming Language</a></div>
<div class="top-heading" id="heading-narrow"><a href="http://localhost:6060/">Go</a></div>
<a href="asm_386.s.html#" id="menu-button"><span id="menu-button-arrow">&#9661;</span></a>
<form method="GET" action="http://localhost:6060/search">
<div id="menu">
<a href="http://localhost:6060/doc/">Documents</a>
<a href="http://localhost:6060/pkg/">Packages</a>
<a href="http://localhost:6060/project/">The Project</a>
<a href="http://localhost:6060/help/">Help</a>
<a href="http://localhost:6060/blog/">Blog</a>

<input type="text" id="search" name="q" class="inactive" value="Search" placeholder="Search">
</div>
</form>

</div></div>



<div id="page" class="wide">
<div class="container">


  <h1>
    Text file
    <a href="http://localhost:6060/src">src</a>/<a href="http://localhost:6060/src/sync">sync</a>/<a href="../atomic.1.html">atomic</a>/<span class="text-muted">asm_386.s</span>
  </h1>





  <h2>
    Documentation: <a href="../../../pkg/sync/atomic.1.html">sync/atomic</a>
  </h2>



<div id="nav"></div>


<pre><span id="L1" class="ln">     1</span>	// Copyright 2011 The Go Authors. All rights reserved.
<span id="L2" class="ln">     2</span>	// Use of this source code is governed by a BSD-style
<span id="L3" class="ln">     3</span>	// license that can be found in the LICENSE file.
<span id="L4" class="ln">     4</span>	
<span id="L5" class="ln">     5</span>	// +build !race
<span id="L6" class="ln">     6</span>	
<span id="L7" class="ln">     7</span>	#include &#34;textflag.h&#34;
<span id="L8" class="ln">     8</span>	
<span id="L9" class="ln">     9</span>	TEXT ·SwapInt32(SB),NOSPLIT,$0-12
<span id="L10" class="ln">    10</span>		JMP	·SwapUint32(SB)
<span id="L11" class="ln">    11</span>	
<span id="L12" class="ln">    12</span>	TEXT ·SwapUint32(SB),NOSPLIT,$0-12
<span id="L13" class="ln">    13</span>		MOVL	addr+0(FP), BP
<span id="L14" class="ln">    14</span>		MOVL	new+4(FP), AX
<span id="L15" class="ln">    15</span>		XCHGL	AX, 0(BP)
<span id="L16" class="ln">    16</span>		MOVL	AX, old+8(FP)
<span id="L17" class="ln">    17</span>		RET
<span id="L18" class="ln">    18</span>	
<span id="L19" class="ln">    19</span>	TEXT ·SwapInt64(SB),NOSPLIT,$0-20
<span id="L20" class="ln">    20</span>		JMP	·SwapUint64(SB)
<span id="L21" class="ln">    21</span>	
<span id="L22" class="ln">    22</span>	TEXT ·SwapUint64(SB),NOSPLIT,$0-20
<span id="L23" class="ln">    23</span>		// no XCHGQ so use CMPXCHG8B loop
<span id="L24" class="ln">    24</span>		MOVL	addr+0(FP), BP
<span id="L25" class="ln">    25</span>		TESTL	$7, BP
<span id="L26" class="ln">    26</span>		JZ	2(PC)
<span id="L27" class="ln">    27</span>		MOVL	0, AX // crash with nil ptr deref
<span id="L28" class="ln">    28</span>		// CX:BX = new
<span id="L29" class="ln">    29</span>		MOVL	new_lo+4(FP), BX
<span id="L30" class="ln">    30</span>		MOVL	new_hi+8(FP), CX
<span id="L31" class="ln">    31</span>		// DX:AX = *addr
<span id="L32" class="ln">    32</span>		MOVL	0(BP), AX
<span id="L33" class="ln">    33</span>		MOVL	4(BP), DX
<span id="L34" class="ln">    34</span>	swaploop:
<span id="L35" class="ln">    35</span>		// if *addr == DX:AX
<span id="L36" class="ln">    36</span>		//	*addr = CX:BX
<span id="L37" class="ln">    37</span>		// else
<span id="L38" class="ln">    38</span>		//	DX:AX = *addr
<span id="L39" class="ln">    39</span>		// all in one instruction
<span id="L40" class="ln">    40</span>		LOCK
<span id="L41" class="ln">    41</span>		CMPXCHG8B	0(BP)
<span id="L42" class="ln">    42</span>		JNZ	swaploop
<span id="L43" class="ln">    43</span>	
<span id="L44" class="ln">    44</span>		// success
<span id="L45" class="ln">    45</span>		// return DX:AX
<span id="L46" class="ln">    46</span>		MOVL	AX, old_lo+12(FP)
<span id="L47" class="ln">    47</span>		MOVL	DX, old_hi+16(FP)
<span id="L48" class="ln">    48</span>		RET
<span id="L49" class="ln">    49</span>	
<span id="L50" class="ln">    50</span>	TEXT ·SwapUintptr(SB),NOSPLIT,$0-12
<span id="L51" class="ln">    51</span>		JMP	·SwapUint32(SB)
<span id="L52" class="ln">    52</span>	
<span id="L53" class="ln">    53</span>	TEXT ·CompareAndSwapInt32(SB),NOSPLIT,$0-13
<span id="L54" class="ln">    54</span>		JMP	·CompareAndSwapUint32(SB)
<span id="L55" class="ln">    55</span>	
<span id="L56" class="ln">    56</span>	TEXT ·CompareAndSwapUint32(SB),NOSPLIT,$0-13
<span id="L57" class="ln">    57</span>		MOVL	addr+0(FP), BP
<span id="L58" class="ln">    58</span>		MOVL	old+4(FP), AX
<span id="L59" class="ln">    59</span>		MOVL	new+8(FP), CX
<span id="L60" class="ln">    60</span>		// CMPXCHGL was introduced on the 486.
<span id="L61" class="ln">    61</span>		LOCK
<span id="L62" class="ln">    62</span>		CMPXCHGL	CX, 0(BP)
<span id="L63" class="ln">    63</span>		SETEQ	swapped+12(FP)
<span id="L64" class="ln">    64</span>		RET
<span id="L65" class="ln">    65</span>	
<span id="L66" class="ln">    66</span>	TEXT ·CompareAndSwapUintptr(SB),NOSPLIT,$0-13
<span id="L67" class="ln">    67</span>		JMP	·CompareAndSwapUint32(SB)
<span id="L68" class="ln">    68</span>	
<span id="L69" class="ln">    69</span>	TEXT ·CompareAndSwapInt64(SB),NOSPLIT,$0-21
<span id="L70" class="ln">    70</span>		JMP	·CompareAndSwapUint64(SB)
<span id="L71" class="ln">    71</span>	
<span id="L72" class="ln">    72</span>	TEXT ·CompareAndSwapUint64(SB),NOSPLIT,$0-21
<span id="L73" class="ln">    73</span>		MOVL	addr+0(FP), BP
<span id="L74" class="ln">    74</span>		TESTL	$7, BP
<span id="L75" class="ln">    75</span>		JZ	2(PC)
<span id="L76" class="ln">    76</span>		MOVL	0, AX // crash with nil ptr deref
<span id="L77" class="ln">    77</span>		MOVL	old_lo+4(FP), AX
<span id="L78" class="ln">    78</span>		MOVL	old_hi+8(FP), DX
<span id="L79" class="ln">    79</span>		MOVL	new_lo+12(FP), BX
<span id="L80" class="ln">    80</span>		MOVL	new_hi+16(FP), CX
<span id="L81" class="ln">    81</span>		// CMPXCHG8B was introduced on the Pentium.
<span id="L82" class="ln">    82</span>		LOCK
<span id="L83" class="ln">    83</span>		CMPXCHG8B	0(BP)
<span id="L84" class="ln">    84</span>		SETEQ	swapped+20(FP)
<span id="L85" class="ln">    85</span>		RET
<span id="L86" class="ln">    86</span>	
<span id="L87" class="ln">    87</span>	TEXT ·AddInt32(SB),NOSPLIT,$0-12
<span id="L88" class="ln">    88</span>		JMP	·AddUint32(SB)
<span id="L89" class="ln">    89</span>	
<span id="L90" class="ln">    90</span>	TEXT ·AddUint32(SB),NOSPLIT,$0-12
<span id="L91" class="ln">    91</span>		MOVL	addr+0(FP), BP
<span id="L92" class="ln">    92</span>		MOVL	delta+4(FP), AX
<span id="L93" class="ln">    93</span>		MOVL	AX, CX
<span id="L94" class="ln">    94</span>		// XADD was introduced on the 486.
<span id="L95" class="ln">    95</span>		LOCK
<span id="L96" class="ln">    96</span>		XADDL	AX, 0(BP)
<span id="L97" class="ln">    97</span>		ADDL	AX, CX
<span id="L98" class="ln">    98</span>		MOVL	CX, new+8(FP)
<span id="L99" class="ln">    99</span>		RET
<span id="L100" class="ln">   100</span>	
<span id="L101" class="ln">   101</span>	TEXT ·AddUintptr(SB),NOSPLIT,$0-12
<span id="L102" class="ln">   102</span>		JMP	·AddUint32(SB)
<span id="L103" class="ln">   103</span>	
<span id="L104" class="ln">   104</span>	TEXT ·AddInt64(SB),NOSPLIT,$0-20
<span id="L105" class="ln">   105</span>		JMP	·AddUint64(SB)
<span id="L106" class="ln">   106</span>	
<span id="L107" class="ln">   107</span>	TEXT ·AddUint64(SB),NOSPLIT,$0-20
<span id="L108" class="ln">   108</span>		// no XADDQ so use CMPXCHG8B loop
<span id="L109" class="ln">   109</span>		MOVL	addr+0(FP), BP
<span id="L110" class="ln">   110</span>		TESTL	$7, BP
<span id="L111" class="ln">   111</span>		JZ	2(PC)
<span id="L112" class="ln">   112</span>		MOVL	0, AX // crash with nil ptr deref
<span id="L113" class="ln">   113</span>		// DI:SI = delta
<span id="L114" class="ln">   114</span>		MOVL	delta_lo+4(FP), SI
<span id="L115" class="ln">   115</span>		MOVL	delta_hi+8(FP), DI
<span id="L116" class="ln">   116</span>		// DX:AX = *addr
<span id="L117" class="ln">   117</span>		MOVL	0(BP), AX
<span id="L118" class="ln">   118</span>		MOVL	4(BP), DX
<span id="L119" class="ln">   119</span>	addloop:
<span id="L120" class="ln">   120</span>		// CX:BX = DX:AX (*addr) + DI:SI (delta)
<span id="L121" class="ln">   121</span>		MOVL	AX, BX
<span id="L122" class="ln">   122</span>		MOVL	DX, CX
<span id="L123" class="ln">   123</span>		ADDL	SI, BX
<span id="L124" class="ln">   124</span>		ADCL	DI, CX
<span id="L125" class="ln">   125</span>	
<span id="L126" class="ln">   126</span>		// if *addr == DX:AX {
<span id="L127" class="ln">   127</span>		//	*addr = CX:BX
<span id="L128" class="ln">   128</span>		// } else {
<span id="L129" class="ln">   129</span>		//	DX:AX = *addr
<span id="L130" class="ln">   130</span>		// }
<span id="L131" class="ln">   131</span>		// all in one instruction
<span id="L132" class="ln">   132</span>		LOCK
<span id="L133" class="ln">   133</span>		CMPXCHG8B	0(BP)
<span id="L134" class="ln">   134</span>	
<span id="L135" class="ln">   135</span>		JNZ	addloop
<span id="L136" class="ln">   136</span>	
<span id="L137" class="ln">   137</span>		// success
<span id="L138" class="ln">   138</span>		// return CX:BX
<span id="L139" class="ln">   139</span>		MOVL	BX, new_lo+12(FP)
<span id="L140" class="ln">   140</span>		MOVL	CX, new_hi+16(FP)
<span id="L141" class="ln">   141</span>		RET
<span id="L142" class="ln">   142</span>	
<span id="L143" class="ln">   143</span>	TEXT ·LoadInt32(SB),NOSPLIT,$0-8
<span id="L144" class="ln">   144</span>		JMP	·LoadUint32(SB)
<span id="L145" class="ln">   145</span>	
<span id="L146" class="ln">   146</span>	TEXT ·LoadUint32(SB),NOSPLIT,$0-8
<span id="L147" class="ln">   147</span>		MOVL	addr+0(FP), AX
<span id="L148" class="ln">   148</span>		MOVL	0(AX), AX
<span id="L149" class="ln">   149</span>		MOVL	AX, val+4(FP)
<span id="L150" class="ln">   150</span>		RET
<span id="L151" class="ln">   151</span>	
<span id="L152" class="ln">   152</span>	TEXT ·LoadInt64(SB),NOSPLIT,$0-12
<span id="L153" class="ln">   153</span>		JMP	·LoadUint64(SB)
<span id="L154" class="ln">   154</span>	
<span id="L155" class="ln">   155</span>	TEXT ·LoadUint64(SB),NOSPLIT,$0-12
<span id="L156" class="ln">   156</span>		MOVL	addr+0(FP), AX
<span id="L157" class="ln">   157</span>		TESTL	$7, AX
<span id="L158" class="ln">   158</span>		JZ	2(PC)
<span id="L159" class="ln">   159</span>		MOVL	0, AX // crash with nil ptr deref
<span id="L160" class="ln">   160</span>		// MOVQ and EMMS were introduced on the Pentium MMX.
<span id="L161" class="ln">   161</span>		// MOVQ (%EAX), %MM0
<span id="L162" class="ln">   162</span>		BYTE $0x0f; BYTE $0x6f; BYTE $0x00
<span id="L163" class="ln">   163</span>		// MOVQ %MM0, 0x8(%ESP)
<span id="L164" class="ln">   164</span>		BYTE $0x0f; BYTE $0x7f; BYTE $0x44; BYTE $0x24; BYTE $0x08
<span id="L165" class="ln">   165</span>		EMMS
<span id="L166" class="ln">   166</span>		RET
<span id="L167" class="ln">   167</span>	
<span id="L168" class="ln">   168</span>	TEXT ·LoadUintptr(SB),NOSPLIT,$0-8
<span id="L169" class="ln">   169</span>		JMP	·LoadUint32(SB)
<span id="L170" class="ln">   170</span>	
<span id="L171" class="ln">   171</span>	TEXT ·LoadPointer(SB),NOSPLIT,$0-8
<span id="L172" class="ln">   172</span>		JMP	·LoadUint32(SB)
<span id="L173" class="ln">   173</span>	
<span id="L174" class="ln">   174</span>	TEXT ·StoreInt32(SB),NOSPLIT,$0-8
<span id="L175" class="ln">   175</span>		JMP	·StoreUint32(SB)
<span id="L176" class="ln">   176</span>	
<span id="L177" class="ln">   177</span>	TEXT ·StoreUint32(SB),NOSPLIT,$0-8
<span id="L178" class="ln">   178</span>		MOVL	addr+0(FP), BP
<span id="L179" class="ln">   179</span>		MOVL	val+4(FP), AX
<span id="L180" class="ln">   180</span>		XCHGL	AX, 0(BP)
<span id="L181" class="ln">   181</span>		RET
<span id="L182" class="ln">   182</span>	
<span id="L183" class="ln">   183</span>	TEXT ·StoreInt64(SB),NOSPLIT,$0-12
<span id="L184" class="ln">   184</span>		JMP	·StoreUint64(SB)
<span id="L185" class="ln">   185</span>	
<span id="L186" class="ln">   186</span>	TEXT ·StoreUint64(SB),NOSPLIT,$0-12
<span id="L187" class="ln">   187</span>		MOVL	addr+0(FP), AX
<span id="L188" class="ln">   188</span>		TESTL	$7, AX
<span id="L189" class="ln">   189</span>		JZ	2(PC)
<span id="L190" class="ln">   190</span>		MOVL	0, AX // crash with nil ptr deref
<span id="L191" class="ln">   191</span>		// MOVQ and EMMS were introduced on the Pentium MMX.
<span id="L192" class="ln">   192</span>		// MOVQ 0x8(%ESP), %MM0
<span id="L193" class="ln">   193</span>		BYTE $0x0f; BYTE $0x6f; BYTE $0x44; BYTE $0x24; BYTE $0x08
<span id="L194" class="ln">   194</span>		// MOVQ %MM0, (%EAX)
<span id="L195" class="ln">   195</span>		BYTE $0x0f; BYTE $0x7f; BYTE $0x00 
<span id="L196" class="ln">   196</span>		EMMS
<span id="L197" class="ln">   197</span>		// This is essentially a no-op, but it provides required memory fencing.
<span id="L198" class="ln">   198</span>		// It can be replaced with MFENCE, but MFENCE was introduced only on the Pentium4 (SSE2).
<span id="L199" class="ln">   199</span>		XORL	AX, AX
<span id="L200" class="ln">   200</span>		LOCK
<span id="L201" class="ln">   201</span>		XADDL	AX, (SP)
<span id="L202" class="ln">   202</span>		RET
<span id="L203" class="ln">   203</span>	
<span id="L204" class="ln">   204</span>	TEXT ·StoreUintptr(SB),NOSPLIT,$0-8
<span id="L205" class="ln">   205</span>		JMP	·StoreUint32(SB)
</pre><p><a href="asm_386.s%3Fm=text">View as plain text</a></p>

<div id="footer">
Build version go1.9.4.<br>
Except as <a href="https://developers.google.com/site-policies#restrictions">noted</a>,
the content of this page is licensed under the
Creative Commons Attribution 3.0 License,
and code is licensed under a <a href="http://localhost:6060/LICENSE">BSD license</a>.<br>
<a href="http://localhost:6060/doc/tos.html">Terms of Service</a> | 
<a href="http://www.google.com/intl/en/policies/privacy/">Privacy Policy</a>
</div>

</div><!-- .container -->
</div><!-- #page -->

<!-- TODO(adonovan): load these from <head> using "defer" attribute? -->
<script type="text/javascript" src="../../../lib/godoc/jquery.js"></script>
<script type="text/javascript" src="../../../lib/godoc/jquery.treeview.js"></script>
<script type="text/javascript" src="../../../lib/godoc/jquery.treeview.edit.js"></script>


<script>var goVersion = "go1.9.4";</script>
<script type="text/javascript" src="../../../lib/godoc/godocs.js"></script>

</body>
</html>

